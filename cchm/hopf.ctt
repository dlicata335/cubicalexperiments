module hopf where

import prelude
import susp
import s1
import join
import pointedtypes


---- Extra prereqs---------------------------------

S2 : U = Susp S1


Omega (Z : PointedType) : PointedType = (Path Z.1 (Z.2) (Z.2), <_> Z.2)

-- Iterated loop space
itOmega : Nat -> PointedType -> PointedType = split
  zero  -> \ (A:PointedType) -> A
  suc n -> \(A:PointedType) -> itOmega n (Omega A)

fibOmega (B : PointedType) (P : B.1 -> U) (f : P (B.2)) (p : (Omega B).1) : U =
 PathP (<i> P (p @ i)) f f

itFibOmega : (n : Nat) (B : PointedType) (P : B.1 -> U) (f : P (B.2)) -> (itOmega n B).1 -> U
 = split
  zero  -> \ (B : PointedType) (P : B.1 -> U) (f : P (B.2)) -> P
  suc n -> \ (B : PointedType) (P : B.1 -> U) (f : P (B.2)) -> itFibOmega n (Omega B) (fibOmega B P f) (refl (P (B.2)) f)

inhOrTrunc (A:U) : Nat -> U = split
  zero  -> A
  suc n -> (x y : A) -> inhOrTrunc (Path A x y) n

subst (A : U) (P : A -> U) (a0 a1 : A) (p : Path A a0 a1) (b : P a0) : P a1 = comp (<x> P (p @ x)) b []

substInv (A : U) (P : A -> U) (a0 a1 : A) (p : Path A a0 a1) (b : P a1) : P a0 = comp (<x> P (p @ -x)) b []

truncFibOmega (B : PointedType) (P : B.1 -> U) (f : P (pt B)) (n : Nat) 
  (tr:inhOrTrunc (P (pt B)) (suc n)) (p : (Omega B).1) : inhOrTrunc (fibOmega B P f p) n 
 = substInv U (\ (X:U) -> inhOrTrunc X n)
    (PathP (<i> P(p @ i)) f f) 
    (Path (P (pt B)) (subst B.1 P (pt B) (pt B) p f) f)
    (<i> PathP (<k> P (p @ (k /\ i)))  
      (fill (<j> P (p @ j)) f [] @ i)  
      f 
    ) 
    (tr (subst B.1 P (pt B) (pt B) p f) f)

-- PathP (<!0> U) (PathP (<!0> P (p @ !0)) f f) (PathP (<!0> P B.2) ? ?)

-- (funDepTr (P (pt B)) (P (pt B)) (<i>P (p@i)) f f)


---------------------------------

hopf : S2 -> U = split
  north -> S1
  south -> S1
  merid a @ x -> rotpath a @ x

totalHopfToJoin : (a : S2) -> hopf a -> Join S1 S1 = split
  north -> \ (a : S1) -> joinl (comp (<_> S1) a [])
  south -> \ (a : S1) -> joinr (comp (<_> S1) a [])
  merid a @ x ->
   \(p : rotpath a @ x) ->
     join{Join S1 S1}
       (comp (<y> rotpath a @ -y /\ x) p [])
       (comp (<y> rotpath a @ y \/ x) p [])
       @ x


S3 : U = Susp S2
ptS3 : PointedType = (S3, north)
ptS2 : PointedType = (S2, north)
ptS1 : PointedType = (S1, base)


hopfOne : (itOmega one ptS2).1 -> U = itFibOmega one ptS2 hopf base

hopfTwo : (itOmega two ptS2).1 -> U = itFibOmega two ptS2 hopf base

hopfThree : (itOmega three ptS2).1 -> U = itFibOmega three ptS2 hopf base

ptJoin (pA:PointedType) (B:U) : PointedType = (Join pA.1 B, joinl (pt pA))


-- fibContrHopfThree (p : (itOmega three ptS2).1) : hopfThree p = 
--   truncFibOmega (itOmega two ptS2) hopfTwo (refl (Id S1 base base) (refl S1 base)) zero
--     (truncFibOmega (Omega ptS2) hopfOne (refl S1 base) one
--           (truncFibOmega ptS2 hopf base two isGroupoidS1 (refl S2 north))
--         (refl (Omega ptS2).1 (pt (Omega ptS2)))) p

